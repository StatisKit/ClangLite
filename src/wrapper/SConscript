# -*-python-*-

import os

Import("env")

wrapper_env = env.Clone()

sources = wrapper_env.ALEAGlob('*.cpp')

target = str(wrapper_env.File("../pyclanglite/__pyclanglite").srcnode()) 

kwargs = dict()

if os.name == 'nt':
    kwargs['SHLIBSUFFIX'] = '.pyd'
else:
    kwargs['SHLIBSUFFIX'] = '.so'

kwargs['SHLIBPREFIX'] = ''

if wrapper_env['compiler'] == 'msvc' and '8.0' in wrapper_env['MSVS_VERSION']:
    kwargs['SHLINKCOM'] = [wrapper_env['SHLINKCOM'],
    'mt.exe -nologo -manifest ${TARGET}.manifest -outputresource:$TARGET;2']

if os.name == 'nt':
    # Fix bug with Scons 0.97, Solved in newer versions.
    wrap = wrapper_env.SharedLibrary(target, sources, **kwargs)
elif os.sys.platform == 'darwin':
    wrap = wrapper_env.LoadableModule(target, sources, LDMODULESUFFIX='.so',
                           FRAMEWORKSFLAGS = '-flat_namespace -undefined suppress', **kwargs)
else:
    wrap = wrapper_env.LoadableModule(target, sources, **kwargs)

Alias("build", wrap)
